actor ZEnemyPainElemental : PainElemental {
	species "ZInvEnemy"
	bloodcolor "gold"
	translation "168:191=160:167", "128:151=200:207", "13:15=240:247", "236:239=240:247", "1:2=240:247", "48:79=192:207", "208:223=160:167"
	health 550
	painchance 50
	+QUICKTORETALIATE
  +DONTHARMSPECIES
  Dropitem "BlueMana", 256
  Dropitem "BlueHeroMana", 256
  var int user_healthbarid;
	states {
		Spawn:
			PAIN A 0
		Idle:
			PAIN A 0 ACS_NamedExecuteAlways("hate something new", 0)
			goto Super::Spawn
		IdleQuiet:
			PAIN A 0 ACS_NamedExecuteAlways("hate something new", 0)
			PAIN A 10 A_LookEx(LOF_NOSEESOUND)
			goto IdleQuiet+1
		ClearTarget:
			PAIN A 0 A_ClearTarget
			goto IdleQuiet
		See:
			PAIN A 0 A_SpawnItemEx("GibbageFlying",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			PAIN AABBCCBBAABBCCBB 3 A_Chase
			PAIN A 0 A_JumpIfInTargetInventory("ImDefinitelyAPlayer", 1, "ClearTarget")
			PAIN A 0 A_JumpIfTargetInLOS("See")
			PAIN A 0 A_Jump(128, "ClearTarget")
			loop
    Missile:
      PAIN D 5 A_FaceTarget
      PAIN E 5 A_FaceTarget
      PAIN F 5 Bright A_FaceTarget
      PAIN F 0 Bright A_PainAttack("ZEnemyLostSoul")
      Goto See
		Melee:
      PAIN D 5 A_FaceTarget
      PAIN E 5 A_FaceTarget
      PAIN F 5 Bright A_FaceTarget
      PAIN F 0 Bright A_CustomMissile("ZPainElementalLostSoulBomb")
      Goto See
		Pain:
			PAIN G 6 Bright
			PAIN G 6 Bright A_Pain
			goto See
		Death:
			PAIN H 8 bright
			PAIN I 8 bright A_Scream
			PAIN JK 8 bright
			PAIN L 8 bright A_PainDie("ZEnemyLostSoul")
			PAIN M 8 bright
			stop
		Raise:
			stop
	}
}

actor ZPainElementalLostSoulBomb {
	height 32
	radius 16
	speed 20
	damage 3
	PROJECTILE
	+DONTREFLECT
	states {
		Spawn:
			SKUL AB 4 bright
			loop
		Death:
			SKUL FG 6 Bright
			SKUL H 6 bright A_Explode(32, 128, 0)
			SKUL I 6 bright
			SKUL JK 6
			Stop
	}
}

actor PainElementalZSpawner : CustomMonsterInvasionSpot 16011
{
	dropitem "ZEnemyPainElemental"
	radius 31
	height 56
	//$Category ZInvasion Spawners
	//$Sprite PAINA2A8
	//$Title Pain Elemental
	//$Arg0 Minimum amount spawned
	//$Arg1 Delay between spawns
	//$Arg2 Delay until first spawn
	//$Arg3 Starts spawning in wave
	//$Arg4 Maximum amount spawned
} 