actor ZEnemyFatso : Fatso {
  species "ZInvEnemy"
	bloodcolor "gold"
	translation "112:127=160:167", "16:47=160:167", "48:79=112:127", "168:191=160:167"
	health 1300
	painchance 10
	+QUICKTORETALIATE
  Dropitem "BlueMana", 256
  Dropitem "BlueHeroMana", 256
  var int user_healthbarid;
	states {
		Spawn:
			FATT A 0
		Idle:
			FATT A 0 ACS_NamedExecuteAlways("hate something new", 0)
			goto Super::Spawn
		IdleQuiet:
			FATT A 0 ACS_NamedExecuteAlways("hate something new", 0)
			FATT AB 15 A_LookEx(LOF_NOSEESOUND)
			goto IdleQuiet+1
		ClearTarget:
			FATT A 0 A_ClearTarget
			goto IdleQuiet
		See:
			FATT A 0 A_SpawnItemEx("XBStepHiTier",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			FATT AABBCC 4 A_Chase
			FATT D 0 A_SpawnItemEx("XBStepHiTier",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
			FATT DDEEFF 4 A_Chase
			FATT A 0 A_JumpIfInTargetInventory("ImDefinitelyAPlayer", 1, "ClearTarget")
			FATT A 0 A_JumpIfTargetInLOS("See")
			FATT A 0 A_Jump(128, "ClearTarget")
			loop
		Missile:
			FATT G 20 A_FatRaise
			FATT H 10 Bright A_FatAttack1
			FATT IG 5 A_FaceTarget
			FATT H 10 Bright A_FatAttack2
			FATT IG 5 A_FaceTarget
			FATT H 10 Bright A_FatAttack3
			FATT IG 5 A_FaceTarget
			goto See
		Pain:
			FATT J 3
			FATT J 3 A_Pain
			goto See
		Raise:
			stop
	}
}

actor ZEnemyGhostFatso : ZEnemyFatso {
	+GHOST
	renderstyle translucent
	alpha 0.5
}

actor FatsoZSpawner : CustomMonsterInvasionSpot 16008
{
	dropitem "ZEnemyFatso"
	radius 48
	height 64
	//$Category ZInvasion Spawners
	//$Sprite FATTA2A8
	//$Title Mancubus
	//$Arg0 Minimum amount spawned
	//$Arg1 Delay between spawns
	//$Arg2 Delay until first spawn
	//$Arg3 Starts spawning in wave
	//$Arg4 Maximum amount spawned
}

actor GhostFatsoZSpawner : CustomMonsterInvasionSpot 16005
{
	dropitem "ZEnemyGhostFatso"
	radius 48
	height 64
	//$Category ZInvasion Spawners
	//$Sprite FATTG2G8
	//$Title Ghost Mancubus
	//$Arg0 Minimum amount spawned
	//$Arg1 Delay between spawns
	//$Arg2 Delay until first spawn
	//$Arg3 Starts spawning in wave
	//$Arg4 Maximum amount spawned
} 