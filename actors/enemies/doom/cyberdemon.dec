actor ZEnemyCyberdemon : Cyberdemon {
  species "ZInvEnemy"
	bloodcolor "gold"
	translation "48:79=[0,255,255]:[0,23,23]", "144:151=[0,51,51]:[0,51,51]", "142:142=[0,67,67]:[0,67,67]", "1:2=0:0", "176:191=[128,0,128]:[8,0,8]", "32:47=[128,0,128]:[8,0,8]"
	health 6000
	mass 5000
	painchance 4
  Damagefactor "Annihilate", 0
  Damagefactor "Ridiculous", 0.6
	-BOSS
	+QUICKTORETALIATE
  +DONTHARMSPECIES
	+NOICEDEATH
	+NODROPOFF
	+DONTRIP
  Dropitem "RedMana", 256
  Dropitem "RedHeroMana", 256
  var int user_healthbarid;
	states {
		Spawn:
			CYBR A 0
			CYBR A 0 A_PlaySound("cyber/sight", CHAN_WEAPON, 1.0, false, ATTN_NONE)
		Idle:
			CYBR A 0 ACS_NamedExecuteAlways("hate something new", 0)
			goto Super::Spawn
		IdleQuiet:
			CYBR A 0 ACS_NamedExecuteAlways("hate something new", 0)
			CYBR AB 10 A_LookEx(LOF_NOSEESOUND)
			goto IdleQuiet+1
		ClearTarget:
			CYBR A 0 A_ClearTarget
			goto IdleQuiet
		See:
			CYBR A 3 A_Hoof
			CYBR ABBCC 3 A_Chase
			CYBR D 3 A_Metal
			CYBR D 3 A_Chase
			CYBR A 3 A_Hoof
			CYBR ABBCC 3 A_Chase
			CYBR D 3 A_Metal
			CYBR D 3 A_Chase
			CYBR A 0 A_JumpIfInTargetInventory("ImDefinitelyAPlayer", 1, "ClearTarget")
			CYBR A 0 A_JumpIfTargetInLOS("See")
			CYBR A 0 A_Jump(128, "ClearTarget")
			loop
		Melee:
		Missile:
			CYBR E 6 A_FaceTarget
			CYBR F 10 A_CyberAttack
			CYBR E 10 A_FaceTarget
			CYBR F 10 A_CyberAttack
			CYBR E 10 A_FaceTarget
			CYBR F 10 A_CyberAttack
			goto See
		Pain:
			CYBR G 10 A_Pain
			goto See
	}
}

actor CyberdemonZSpawner : CustomMonsterInvasionSpot 16007
{
	dropitem "ZEnemyCyberdemon"
	radius 40
	height 110
	//$Category ZInvasion Spawners
	//$Sprite CYBRA2
	//$Title Cyberdemon
	//$Arg0 Minimum amount spawned
	//$Arg1 Delay between spawns
	//$Arg2 Delay until first spawn
	//$Arg3 Starts spawning in wave
	//$Arg4 Maximum amount spawned
} 